apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.jtcstudios.calculator"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 2
        versionName "1.0.1"
        setProperty("archivesBaseName", "calculator")
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "variants"
    productFlavors {
        core {}
        fdroid {}
        prepaid {}
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'com.simplemobiletools.calculator'

}

dependencies {
    implementation 'com.github.JTCJTCC:JT-Framework:225b8254fe'
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'net.objecthunter:exp4j:0.4.8'

    kapt 'androidx.room:room-compiler:2.5.0'
    implementation 'androidx.room:room-runtime:2.5.0'
    annotationProcessor 'androidx.room:room-compiler:2.5.0'
}
